<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:fc="http://www.adobe.com/2006/fc"
	layout="absolute" creationComplete="init()" viewSourceURL="srcview/index.html" width="653">

	
	<mx:Style>
		.text {
			color: #222222;
			font-family: Verdana;
		}
	</mx:Style>
	<fc:AutoComplete id="txtSearch"  labelField="name" x="123" y="13" width="285" keyDown="{if (event.keyCode == 13) search();}" />

	<fc:SpringGraph id="s" backgroundColor="#0" 
		lineColor="#333388" left="0" right="0" top="69" bottom="0"
		itemRenderer="RelationItemView" repulsionFactor="{rep.value}"
		 >
	</fc:SpringGraph>
	
	<mx:Label x="10" y="10" text="Movie Cube" styleName="text" fontSize="15"/>
	<mx:Label x="123" y="43" fontSize="10" styleName="text" id="help"/>
	
	<mx:Script>
		<![CDATA[
		import com.adobe.flex.extras.controls.springgraph.Graph;	
		import com.adobe.serialization.json.JSON;  
		import mx.controls.Alert;
				
		[Bindable]
		public var scaleFactor: Number = 1.0;
		
		private var items: Graph;
		private var serviceUrl:String = "http://localhost:49899/Relation.aspx";

		
		[Bindable]
		private var keys:Array = null;

		public function addItem(data:Object,isStar:Boolean, linkTo: RelationItem): RelationItem {
			var newItem: RelationItem = new RelationItem(data,isStar);
			items.add(newItem);
			
			if(linkTo != null)
				items.link(newItem, linkTo);
			s.dataProvider = items;
			return newItem;
		}
		
	   private function init():void
	   {
	   		items = new Graph();
		    
		    loadKeys();
		     
		    //loadStar("刘德华");
	   }
	   
	   private function loadKeys():void
	   {
	   		var request:URLRequest = new URLRequest(serviceUrl);
		    request.method = URLRequestMethod.POST;
		    var data:URLVariables = new URLVariables();
		    data.type = "queryKeys";
		    request.data = data;
		    var loader:URLLoader = new URLLoader();
		    loader.load(request);
		    loader.addEventListener(Event.COMPLETE,onLoadKeyComplete);
		    loader.addEventListener(IOErrorEvent.IO_ERROR, errorHandler);
	   }
	   
	   public function loadStar(name:String):void
	   {
	   		var myLoader:URLLoader = new URLLoader();
			var myRequest:URLRequest;
	   		help.text = "加载明星 "+name+" 中...";
	   		myRequest = new URLRequest(serviceUrl);
		    myRequest.method = URLRequestMethod.POST;
		    var data:URLVariables = new URLVariables();
		    data.type = "queryStarByName";
		    data.query = name; 
		    myRequest.data = data;
		    myLoader.load(myRequest);
		    myLoader.addEventListener(Event.COMPLETE,onLoadStarComplete);
		    myLoader.addEventListener(IOErrorEvent.IO_ERROR, errorHandler);
	   }
	   
	   public function loadMovie(name:String):void
	   {
	   		help.text = "加载电影 "+name+" 中...";
	   		
	   		var myLoader:URLLoader = new URLLoader();
			var myRequest:URLRequest;
	   		myRequest = new URLRequest(serviceUrl);
		    myRequest.method = URLRequestMethod.POST;
		    var data:URLVariables = new URLVariables();
		    data.type = "queryMovieByStar";
		    data.query = name; 
		    myRequest.data = data;
		    myLoader.load(myRequest);
		    myLoader.addEventListener(Event.COMPLETE,onLoadMovieComplete);
		    myLoader.addEventListener(IOErrorEvent.IO_ERROR, errorHandler);
	   }
	   
	   private function addMovieArray(arr:Array,item:RelationItem):void
	   {
	   		if(arr == null)
	   			return; 
	   		for(var j:int = 0;j<arr.length;++j)
			{
				var asin: String = arr[j].ID;
				var newItem:RelationItem = items.find(asin) as RelationItem;
				if (newItem ==null)
					newItem = addItem(arr[j],false, item);
				else if (item!=null)
					items.link(newItem, item);
					
				addStarArray(arr[j].Actors,newItem);
			}
	   }
	   
	   private function addStarArray(arr:Array,item:RelationItem):void
	   {
	   		if(arr == null)
	   			return; 
	   		for(var j:int = 0;j<arr.length;++j)
			{
				var asin: String = arr[j].ID;
				var newItem:RelationItem = items.find(asin) as RelationItem;
				if (newItem ==null)
					newItem = addItem(arr[j],true, item);
				else if (item!=null)
					items.link(newItem, item);
				
				addMovieArray(arr[j].DirectMovies,newItem);
				addMovieArray(arr[j].WriteMovies,newItem);
				addMovieArray(arr[j].ActMovies,newItem);
		}
	   }
   
	   private function onLoadStarComplete(event:Event):void
	   {
	   		help.text += "完成";
		    var loader:URLLoader = URLLoader(event.target);
		  	var arr:Array = (JSON.decode(loader.data) as Array);
		  	addStarArray(arr,null);
	   }
	   
	   private function errorHandler(e:IOErrorEvent):void
	   {
	   		this.help.text = "加载失败";
	   		  		
	   }
	   
	   private function onLoadMovieComplete(event:Event):void
	   {
	   		help.text += "完成";
		    var loader:URLLoader = URLLoader(event.target);
		  	var arr:Array = (JSON.decode(loader.data) as Array);
		  	addMovieArray(arr,null);
	   }
		
		private function onLoadKeyComplete(event:Event):void
		{
			var loader:URLLoader = URLLoader(event.target);
			keys = (JSON.decode(loader.data) as Array);
			txtSearch.dataProvider=keys;
			txtSearch.labelField = "Key";
			this.help.text = "加载关键字成功";
		}
		
		private function search():void
		{
			items.empty();
			this.loadStar(this.txtSearch.text);
		}

		]]>
	</mx:Script>
	<mx:Button x="416" y="13" label="搜索" id="btnSearch" click="{search();}" 
		/>
	<mx:HSlider id="rep" x="416" y="43" value="0.14" minimum="0.05" maximum="1.5" liveDragging="true" change="s.refresh();" width="101"/>
	<mx:HSlider x="525" y="43" value="1.0" minimum="0.05" maximum="1.2" liveDragging="true" change="scaleFactor = event.currentTarget.value; s.refresh();" width="106"/>
</mx:Application>
