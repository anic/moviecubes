<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="120" height="110" initialize="{onInit();}"
		 xmlns:local="*" alpha="0" >
	<mx:Script>
		<![CDATA[
			import mx.messaging.AbstractConsumer;
			import mx.core.Application;
			
			private var currentView:RelationItemView = null;
			private var toolBtns:Array;
			
			private var timer:Timer;
			
			//每页显示个数
			
			private function onInit():void
			{
				toolBtns = [btnLook,btnSearch,btnPre,btnNext,btnClose];
				timer = new Timer(1000);
				timer.addEventListener(TimerEvent.TIMER, onTimer);
				
			} 
			
			public function onSelected(itemView:RelationItemView):void
			{
				if (itemView == null)
					return;
				if (currentView!=null && currentView!=itemView)
				{
					(currentView.data as RelationItem).onSelected = false;
				}
				
				timer.stop();
				currentView = itemView;
				(currentView.data as RelationItem).onSelected = true;
				
				RelationGraph(Application.application).setToolPanelPosition(currentView.x,currentView.y);
				this.setVisible2(true);
					
			}
			
			public function onNotSelected(itemView:RelationItemView):void
			{
				if (currentView == itemView)
				{
    				timer.start();
				}
			}
			
			private function onTimer(event:TimerEvent):void{
				this.hide();
				timer.stop();
    		}
			
			public function onItemRemove(item:RelationItem):void
			{
				if (currentView == null)
					return;
					
				if (currentView.data == item)
					{
						if (currentView!=null)
						{
							(currentView.data as RelationItem).onSelected = false;
						}
						currentView = null;
						this.setVisible2(false);
					}
										
			}
			
			public function hide():void
			{
				if (currentView!=null)
				{
					(currentView.data as RelationItem).onSelected = false;
				}
				currentView = null;
				this.setVisible2(false);		
			}
			
			
			private function onSearch():void
			{
				if (currentView!=null)
					currentView.searchTA(true);
			}
			
			private function onLook():void
			{
				if (currentView!=null)
				{
					//navigateToURL(new URLRequest('http://www.g.cn/search?q='+currentView.data.name),'_blank');		
					navigateToURL(new URLRequest("./Search.aspx?query="+encodeURI(currentView.data.name)),'_blank');
				}
			} 
			
			private function onClose():void
			{
				if (currentView!=null)
					currentView.removeItem();
			}
			
			public function setVisible2(value:Boolean):void
			{
				if (!value)
				{
					if (!this.allOut.isPlaying)
					{
						this.allOut.play();
					}
				}
				else
				{
					if(this.allOut.isPlaying)
						this.allOut.stop();
					
					this.btnPre.visible = (this.currentView!=null && this.currentView.data.rank == 0);
					this.btnNext.visible = (this.currentView!=null && this.currentView.data.rank == 0);
							
					this.alpha = 1.0;
				}
			}
			
			private function onSearchNextPage(next:Boolean):void
			{
				var countPerPage:int = RelationGraph(Application.application).CountPerPage;
				if (this.currentView!=null)
				{
					var item:RelationItem = (currentView.data as RelationItem);
					if(next)
					{
						var total:int = item.TotalNum;
						item.nextStart = item.start + countPerPage < total? item.start+countPerPage:item.start;
					}	
					else 
					{
						item.nextStart = item.start - countPerPage>=0? item.start - countPerPage:0;
					}
					
					//无需翻页
					if (item.nextStart == item.start)
					{
						return;
					}
					
					if (item.nextStart < item.DataLength)
					{
						//不需要访问网络
						item.start = item.nextStart;
						var ids:Array = item.getRelatedIds();
						RelationGraph(Application.application).removeOldPage(ids,item);
						RelationGraph(Application.application).autoDelete();
						
						var dataArray:Array = new Array();
						var dataObject:Object = new Object();
						var len:int = item.getCurrentPageCount(countPerPage);
						for(var i:int = 0;i<len;++i)
							dataArray.push(item.getArray()[i+item.start]);
						
						dataObject.ObjectType = item.data.ObjectType;
						if (item.isStar)
							dataObject.Movies = dataArray;
						else
							dataObject.Stars = dataArray;
							
						RelationGraph(Application.application).visitRelated(dataObject,item,item.rank,0);
						item.view.onDataUpdate();
						RelationGraph(Application.application).showPageTip(item);
						
					}
					else
					{
						if (item.isStar)
							RelationGraph(Application.application).queryPage("queryStarById",item.data.ID,item.nextStart,countPerPage);
						else
							RelationGraph(Application.application).queryPage("queryMovieById",item.data.ID,item.nextStart,countPerPage);
					}
						
				}
			}
			
			private var onTool:Boolean = true;
			private function onEnter():void
			{
				onTool = true
				this.onSelected(this.currentView);
				this.timer.stop();
			}
			
			private function onOut():void
			{
				onTool = false;
				this.onNotSelected(this.currentView);
			}
			
			private function onPen():void
			{
				if (currentView!=null)
				{
					RelationGraph(Application.application).addToSearch(currentView.data.name);
				}
			}
			
		]]>
	</mx:Script>
	<mx:Style>
		ToolButton
		{
			width:20;
			height:20;
		}
	</mx:Style>
	<mx:Fade alphaFrom="1.0" alphaTo="0" id="allOut" duration="500" target="{this}" startDelay="100" />
	
		
	<local:ToolButton x="0"  ImageIndex="0" toolTip="前一页" id="btnPre" bottom="20" click="{onSearchNextPage(false);}" mouseOver="{onEnter();}" mouseOut="{onOut();}"/>
	<local:ToolButton x="20" ImageIndex="1" toolTip="后一页" id="btnNext" bottom="20" click="{onSearchNextPage(true);}"  mouseOver="{onEnter();}" mouseOut="{onOut();}"/>
	<local:ToolButton x="40" ImageIndex="3" toolTip="页面中查看" id="btnLook" click="{onLook();}" bottom="20" mouseOver="{onEnter();}" mouseOut="{onOut();}"/>
	<local:ToolButton x="60" ImageIndex="4" toolTip="重新加载" id="btnSearch" click="{onSearch();}" bottom="20" mouseOver="{onEnter();}" mouseOut="{onOut();}"/>
	<local:ToolButton x="80" ImageIndex="5" toolTip="添加到搜索栏" id="btnPen" click="{onPen();}" bottom="20" mouseOver="{onEnter();}" mouseOut="{onOut();}"/>
	<local:ToolButton x="100" ImageIndex="2" toolTip="关闭" id="btnClose" click="{onClose();}" bottom="20" mouseOver="{onEnter();}" mouseOut="{onOut();}"/>
	
	
</mx:Module>
